#Register new biomes for Mars
Tweaker.setScriptStage("BIOME_REGISTRY")
Tweaker.createBiome("polar", "minecraft:ice_flats")
Tweaker.createBiome("basin", "minecraft:frozen_ocean")
Tweaker.createBiome("volcano", "biomesoplenty:mountain")

Tweaker.setScriptStage("FINISHED_LOAD")
Tweaker.setPlacementStage("BIOME_BLOCKS")
polar = forBiomes("biometweaker:polar")
basin = forBiomes("biometweaker:basin")
dunes = forBiomes("advancedrocketry:hotdryrock")
volcano = forBiomes("biometweaker:volcano")
coldBeach = forBiomes("minecraft:cold_beach")
basalt = forBlock("projectred-exploration:stone")
basalt.setProperty("type", "basalt")

#Specifications for the polar biome
coldBeach.set("genInitialSnow", false)
coldBeach.set("name", "Frozen Slope")
coldBeach.removeAllSpawns("MONSTER")
polar.remove()
polar.set("name", "Ice Cap")
polar.set("topBlock", "minecraft:snow")
polar.set("fillerBlock", "minecraft:packed_ice")
polar.set("oceanTopBlock", "advancedrocketry:hotturf")
polar.set("genInitialSnow", false)
polar.set("temperature", 0.3)
polar.set("humidity", 0)
polar.removeAllSpawns("MONSTER")

#Specifications for the basin biome
basin.remove()
basin.set("name", "Valley")
basin.set("topBlock", "advancedrocketry:hotturf")
basin.set("fillerBlock", "minecraft:clay")
basin.set("oceanTopBlock", "advancedrocketry:hotturf")
basin.set("genInitialSnow", false)
basin.set("temperature", 0.3)
basin.set("humidity", 0)
basin.set("height", -1.5)
basin.removeAllSpawns("MONSTER")

#Specifications for the dunes biome
dunes.remove()
dunes.set("name", "Dunes")
dunes.set("topBlock", "advancedrocketry:hotturf")
dunes.set("fillerBlock", "advancedrocketry:hotturf")
dunes.set("oceanTopBlock", "advancedrocketry:hotturf")
dunes.set("genInitialSnow", false)
dunes.set("temperature", 0.3)
dunes.removeAllSpawns("MONSTER")

#Specifications for the volcano biome
volcano.remove()
volcano.set("name", "Ancient Volcano")
volcano.set("topBlock", "advancedrocketry:hotturf")
volcano.set("fillerBlock", basalt)
volcano.set("oceanTopBlock", basalt)
volcano.set("genInitialSnow", false)
volcano.set("temperature", 0.3)
volcano.set("humidity", 0)
volcano.set("height", 3)
volcano.set("heightVariation", 0.75)
volcano.removeAllSpawns("MONSTER")

#Replace stone, sand and water
Tweaker.setScriptStage("SERVER_STARTING")
Tweaker.setWorld(103)
Tweaker.setPlacementStage("PRE_POPULATE")
allBiomes = forAllBiomes()
mars = forBiomes("biometweaker:polar","biometweaker:basin","advancedrocketry:hotdryrock","minecraft:beaches","minecraft:cold_beach")
allBiomes.set("enableRain", false)
blockRep = newBlockReplacement()
blockRep.set("minY", 0)
blockRep.set("maxY", 256)
blockRep.set("ignoreMeta", true)
blockRep.set("block", "quark:jasper")
blockRep2 = newBlockReplacement()
blockRep2.set("minY", 0)
blockRep2.set("maxY", 45)
blockRep2.set("ignoreMeta", true)
blockRep2.set("block", basalt)
blockRep3 = newBlockReplacement()
blockRep3.set("minY", 0)
blockRep3.set("maxY", 256)
blockRep3.set("ignoreMeta", true)
blockRep3.set("block", basalt)

allBiomes.registerGenBlockRep("minecraft:water", blockRep)
allBiomes.registerGenBlockRep("minecraft:lava", blockRep)
allBiomes.registerGenBlockRep("minecraft:dirt", blockRep)
allBiomes.registerGenBlockRep("minecraft:gravel", blockRep)
mars.registerGenBlockRep(1, "minecraft:stone", blockRep)
mars.registerGenBlockRep(3, "minecraft:stone", blockRep2)
mars.registerGenBlockRep("minecraft:sandstone", "advancedrocketry:hotturf")
mars.registerGenBlockRep("minecraft:sand", "advancedrocketry:hotturf")
volcano.registerGenBlockRep("minecraft:stone", blockRep3)